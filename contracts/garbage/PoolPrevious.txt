//SPDX-License-Identifier: Unlicense
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/utils/math/SafeMath.sol";
import {WadRayMath} from '../libraries/WadRayMath.sol';
import {IPoolAddressesProvider} from './IPoolAddressesProvider.sol';
import {PoolStorage} from './PoolStorage.sol';
import {IPool} from './IPool.sol';
import {PoolLogic} from '../libraries/PoolLogic.sol';
import {ICollateralToken} from '../tokens/ICollateralToken.sol';
import {IAvaToken} from '../tokens/IAvaToken.sol';
import {IDebtToken} from '../tokens/IDebtToken.sol';
import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";
import "@openzeppelin/contracts-upgradeable/proxy/utils/UUPSUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol";
import {SafeERC20Upgradeable} from "@openzeppelin/contracts-upgradeable/token/ERC20/utils/SafeERC20Upgradeable.sol";
import {IERC20Upgradeable} from "@openzeppelin/contracts-upgradeable/token/ERC20/IERC20Upgradeable.sol";
import {IRewardsTokenBase} from '../tokens/IRewardsTokenBase.sol';

import "hardhat/console.sol";

// actual
// Vaults like DAI/UST are required because debt rates are determined by collateral to ensure
// the return on collateral alone is higher than the debt repayments
contract Pool is Initializable, UUPSUpgradeable, OwnableUpgradeable, IPool, PoolStorage {
    using SafeMath for uint256;
    using SafeERC20Upgradeable for IERC20Upgradeable;
    using WadRayMath for uint256;

    modifier whenNotPaused() {
        require(!paused, "Errors: Pool paused");
        _;
    }

    function getAddressesProvider() external view override returns (address) {
        // in storage
        return address(addressesProvider);
    }

    function initialize(IPoolAddressesProvider provider) external initializer {
        addressesProvider = provider;
    }

    function _authorizeUpgrade(address) internal override onlyOwner {}

    // bonus
    function depositCollateralAndBorrow(
        address _collateralAsset, //
        address _debtAsset, // wrapp to borrow
        address account, // on behalf of
        uint256 amount, // collateral amount
        uint256 bonusFactor
    ) external override whenNotPaused {
        PoolAsset storage collateralAsset = poolAssets[_collateralAsset];
        PoolAsset storage debtAsset = poolAssets[_debtAsset];

        PoolLogic.accrueInterest(
            collateralAsset
        );

        PoolLogic.accrueInterest(
            debtAsset
        );

        uint256 bonusAmount = PoolLogic.getBonusAmount(
            collateralAsset,
            msg.sender,
            bonusFactor
        );

        uint256 borrowAmount = amount.add(bonusAmount);

        PoolLogic.validateDepositCollateralAndBorrow(
            collateralAsset,
            debtAsset,
            addressesProvider.getPriceOracle(),
            amount,
            /* msg.sender, */
            borrowAmount
        );
        // transfer in asset to collateral instance
        IERC20Upgradeable(collateralAsset.asset).safeTransferFrom(msg.sender, collateralAsset.collateralAssetWrapped, amount);

        // mint token
        // send assets to anchor
        // we use bonus factor instead of amount+bonus to accuratly supply bridge with `amount`
        uint256 amountBack = ICollateralToken(collateralAsset.collateralAssetWrapped).mint(
            msg.sender,
            amount,
            collateralAsset.collateralExchangeRate,
            bonusFactor
        );

        //update amount for amountBack
        // if amount back is > === allow higher collateral principal luck
        borrowAmount = amountBack > amount ? borrowAmount : amountBack.add(bonusAmount);

        borrowAmount = PoolLogic.getValueMatchAmount(
          addressesProvider.getPriceOracle(),
          borrowAmount,
          collateralAsset.decimals,
          debtAsset
        );

        // send out asset 1:1
        // mint token
        IDebtToken(debtAsset.debtWrappedAsset).mint(
            msg.sender,
            borrowAmount,
            debtAsset.borrowExchangeRate
        );

        // transfer underlying to borrower
        IAvaToken(debtAsset.wrapped).transferUnderlyingTo(msg.sender, borrowAmount);

        // send savings asset if available
        // may not need if only depositor can initiate this
        // if rate too high, depositors will adjust it
        // withrawing has high fees that may affect performance
        /* rebalance(
            debtAsset.asset
        ); */

        emit DepositCollateralAndBorrow(
            msg.sender,
            _collateralAsset,
            _debtAsset,
            amount,
            borrowAmount // amount can be differentt due to bonus
        );

    }

    function depositSavings(
        address account,
        address asset, // savings asset
        uint256 amount
    ) external whenNotPaused override {
        PoolAsset storage poolAsset = poolAssets[asset];

        // dont check amount, bridge reverts is min not hit
        PoolLogic.validateDepositSavings(
            poolAsset
        );
        // update exchangeRate
        // get rate

        PoolLogic.accrueInterest(
            poolAsset
        );

        // send underlying deposited to wrapped token contract
        // transfer from msg.sender
        IERC20Upgradeable(asset).safeTransferFrom(msg.sender, poolAsset.wrapped, amount);

        // how much to send to anchor
        // accounts for bridging fees
        uint256 bridgeSupplyAmount = PoolLogic.bridgeSupplyOnDepositSavings(
            poolAsset,
            amount
        );

        /* poolAsset.totalDepositsLendable = poolAsset.totalDepositsLendable.add(amount); // used to track av */

        // mint token to account
        IAvaToken(poolAsset.wrapped).mint_(
            account,
            amount.sub(bridgeSupplyAmount),
            bridgeSupplyAmount,
            poolAsset.overallExchangeRate
        );

        emit DepositSavings(
            msg.sender,
            asset,
            amount
        );

    }

    // repay debt and withdraw collateral
    // repay amount in 1:1
    // if debt value is 0 then all is released
    function repayAndWithdraw(
        address _collateralAsset,
        address _debtAsset,
        address account,
        uint256 amount
    ) external whenNotPaused override {
        PoolAsset storage collateralAsset = poolAssets[_collateralAsset];
        PoolAsset storage debtAsset = poolAssets[_debtAsset];

        PoolLogic.accrueInterest(
            collateralAsset
        );

        PoolLogic.accrueInterest(
            debtAsset
        );

        (uint256 debt, uint256 repayAmount) = PoolLogic.confirmRepayAmount(
            debtAsset.debtWrappedAsset,
            amount
        );
        console.log("repayAndWithdraw debt", debt);
        PoolLogic.validateRepay(
            debtAsset,
            collateralAsset,
            repayAmount,
            debt
        );

        // user must have no debt to receive interest
        uint256 totalDebt = PoolLogic.getUserTotalDebt(
            poolAssets,
            savingsAssetsList,
            savingsAssetsCount,
            msg.sender,
            debtAsset.decimals
        );

        uint256 collateralBurnAmount = PoolLogic.getValueMatchAmount(
            addressesProvider.getPriceOracle(),
            repayAmount,
            debtAsset.decimals,
            collateralAsset
        );


        bool redeemFull = false;
        if (repayAmount >= totalDebt) {
            // release all collateral when all debt is zero
            collateralBurnAmount = IERC20Upgradeable(collateralAsset.collateralAssetWrapped).balanceOf(msg.sender);
            redeemFull = true;
        }

        ICollateralToken(collateralAsset.collateralAssetWrapped).burn(msg.sender, msg.sender, collateralBurnAmount, collateralAsset.collateralExchangeRate, redeemFull);

        // allows borrower to withdraw collateral on 0 debt balance
        // can happen on full liquidations
        if (repayAmount > 0) {
            console.log("repayAndWithdraw repayAmount", repayAmount);
            IDebtToken(debtAsset.debtWrappedAsset).burn(msg.sender, repayAmount, debtAsset.borrowExchangeRate);
            // transfer in debt asset to repay
            IERC20Upgradeable(_debtAsset).safeTransferFrom(msg.sender, debtAsset.wrapped, repayAmount);
        }

        // may not need here sine debt repay is accounted for
        emit RepayAndWithdraw(
            msg.sender,
            _collateralAsset,
            _debtAsset,
            repayAmount
        );

    }

    function withdrawSavings(
        address account,
        address asset,
        uint256 amount,
        bool emergency
    ) external whenNotPaused override {
        PoolAsset storage poolAsset = poolAssets[asset];

        PoolLogic.accrueInterest(
            poolAsset
        );

        // amount to withdraw from anchor and send directly to user
        // this is the difference between current amount in anchor minus amount required in anchor after withdraw
        uint256 redeemToAccountAmount = PoolLogic.bridgeRemovalOnWithdrawSavings(
            poolAsset,
            amount
        );

        PoolLogic.validateWithdrawSavings(
            poolAsset,
            msg.sender,
            amount,
            redeemToAccountAmount
            /* emergency */
        );

        if (redeemToAccountAmount != 0) {
            IAvaToken(poolAsset.wrapped)._redeem(
                poolAsset.bridgeAddress,
                redeemToAccountAmount,
                msg.sender,
                emergency
            );
            /* if (emergency) {
                IAvaToken(poolAsset.wrapped).emergencyRedeem(
                    poolAsset.bridgeAddress,
                    redeemToAccountAmount,
                    msg.sender
                );
            } else {
                IAvaToken(poolAsset.wrapped).redeem(
                    poolAsset.bridgeAddress,
                    redeemToAccountAmount,
                    msg.sender
                );
            } */
        }

        // redeemToAccountAmount doesn't inlude fees therefor can be reduced from totalDepositsLendable
        /* poolAsset.totalDepositsLendable = poolAsset.totalDepositsLendable.sub(amount.sub(redeemToAccountAmount)); // used to track av */
        IAvaToken(poolAsset.wrapped).burn_(
            msg.sender,
            amount,
            redeemToAccountAmount,
            poolAsset.overallExchangeRate
        );

        emit WithdrawSavings(
            msg.sender,
            asset,
            amount
        );
    }

    function updateTotalDepositsLendable(address asset, uint256 amountAdded, uint256 amountRemoved) external override {
        PoolAsset storage poolAsset = poolAssets[asset];
        poolAsset.totalDepositsLendable = poolAsset.totalDepositsLendable.add(amountAdded).sub(amountRemoved);
    }

    struct liquidationCallParams {
        address borrower;
        address _debtAsset;
        address _collateralAsset;
    }

    function liquidationCall(
        address borrower,
        address _debtAsset,
        address _collateralAsset,
        uint256 amount
    ) external override whenNotPaused {
        liquidationCallParams memory params;
        params.borrower = borrower;
        params._debtAsset = _debtAsset;
        params._collateralAsset = _collateralAsset;

        PoolStorage.PoolAsset storage debtAsset = poolAssets[params._debtAsset];
        PoolStorage.PoolAsset storage collateralAsset = poolAssets[params._collateralAsset];

        PoolLogic.accrueInterest(
            collateralAsset
        );

        PoolLogic.accrueInterest(
            debtAsset
        );

        (bool canLiquidate , , ) = PoolLogic.canLiquidate(
            poolAssets,
            poolAssetsList,
            poolAssetsCount,
            params.borrower,
            addressesProvider.getPriceOracle()
        );

        /* (uint256 borrowerDebt, uint256 borrowerCollateral) = PoolLogic.getUserAssetDebtAndCollateral(params.borrower, debtAsset.debtWrappedAsset, collateralAsset.collateralAssetWrapped); */

        uint256 borrowerDebt = IERC20Upgradeable(debtAsset.debtWrappedAsset).balanceOf(params.borrower);

        uint256 borrowerCollateral = IERC20Upgradeable(collateralAsset.collateralAssetWrapped).balanceOf(params.borrower);

        PoolLogic.validateLiquidationCall(
            collateralAsset,
            debtAsset,
            borrowerDebt,
            canLiquidate
        );

        /* (uint256 bonus, uint256 _amount) = PoolLogic.updateLiquidationAmounts() */

        // if liquidatees debt less than min threshold for full 100% liquidation, then max is all debt
        // if not then max if debt * max factor
        uint256 maxRepay = borrowerDebt <= debtAsset.minDebtThreshold ? borrowerDebt : borrowerDebt.wadMul(debtAsset.maxDebtRepayFactor);

        if (amount > maxRepay) {
            amount = maxRepay;
        }

        // amount to be released to receiver and burn from borrowers collateral
        // convert to collateral asset decimals
        uint256 amountAsCollateral = amount.mul(10**collateralAsset.decimals).div(10**debtAsset.decimals);
        // bonus is amount+bonus
        uint256 bonus = amountAsCollateral.add(amountAsCollateral.wadMul(collateralAsset.liquidationBonusFactor));

        bool redeemFull = false;
        if (borrowerCollateral < bonus) {
            bonus = borrowerCollateral;
            redeemFull = true;
        }
        // burn and redeem
        ICollateralToken(collateralAsset.collateralAssetWrapped).burn(params.borrower, msg.sender, bonus, collateralAsset.collateralExchangeRate, redeemFull);
        IDebtToken(debtAsset.debtWrappedAsset).burn(params.borrower, amount, debtAsset.borrowExchangeRate);

        // transfer in repay
        IERC20Upgradeable(params._debtAsset).safeTransferFrom(msg.sender, debtAsset.wrapped, amount);

        emit LiquidationCall(
            _collateralAsset,
            _debtAsset,
            amount,
            bonus
        );

    }

    function getPoolAssetData(address asset)
        external
        view
        override
        returns (PoolStorage.PoolAsset memory)
    {
        return poolAssets[asset];
    }

    // check if borrower can be liquidated
    function getUserAccountData(address user) external view returns (bool, uint256, uint256) {
        (bool canLiquidate, uint256 avgLiquidationThreshold, uint256 currentLtv) = PoolLogic.canLiquidate(
            poolAssets,
            poolAssetsList,
            poolAssetsCount,
            user,
            addressesProvider.getPriceOracle()
        );
        return (canLiquidate, avgLiquidationThreshold, currentLtv);
    }

    modifier onlyPoolAdmin() {
        require(msg.sender == addressesProvider.getPoolAdmin());
        _;
    }

    modifier onlyCollateralTokenFactory() {
        require(msg.sender == addressesProvider.getCollateralTokenFactory());
        _;
    }

    modifier onlySavingsTokenFactory() {
        require(msg.sender == addressesProvider.getSavingsTokenFactory());
        _;
    }


    modifier onlyDebtTokenFactory() {
        require(msg.sender == addressesProvider.getDebtTokenFactory());
        _;
    }

    /* modifier onlyRewardsTokenFactory() {
        require(msg.sender == addressesProvider.getRewardsTokenFactory());
        _;
    } */

    // get collateralAssets

    function updateBridgeAddress(address asset, address _bridgeAddress) external onlyPoolAdmin {
        poolAssets[asset].bridgeAddress = _bridgeAddress;
    }

    function updateLtv(address asset, uint256 _ltv) external onlyPoolAdmin {
        poolAssets[asset].ltv = _ltv;
    }

    function updateReserve(address asset, address _reserve) external onlyPoolAdmin {
        poolAssets[asset].reserve = _reserve;
    }

    function updateReserveFactor(address asset, uint256 _reserveFactor) external onlyPoolAdmin {
        poolAssets[asset].reserveFactor = _reserveFactor;
    }

    function updateIsOn(address asset, bool _isActive) external onlyPoolAdmin {
        poolAssets[asset].on = _isActive;
    }

    function updateLendableExchangeRateFactor(address asset, uint256 _lendableExchangeRateFactor) external onlyPoolAdmin {
        poolAssets[asset].lendableExchangeRateFactor = _lendableExchangeRateFactor;
    }

    function updateCollateralExchangeRateFactor(address asset, uint256 _collateralExchangeRateFactor) external onlyPoolAdmin {
        poolAssets[asset].collateralInterestRateFactor = _collateralExchangeRateFactor;
    }

    function updateDebtInterestRateFactor(address asset, uint256 _debtInterestRateFactor) external onlyPoolAdmin {
        poolAssets[asset].debtInterestRateFactor = _debtInterestRateFactor;
    }

    /* function initRewards(
        address rewardsBase,
        address wrapped
    ) external override onlyPoolAdmin {
        IRewardsTokenBase(wrapped).setRewards(
            rewardsBase
        );
    } */

    function initCollateralToken(
        address asset,
        address wrapped,
        address bridgeAddress,
        /* address bridgeExchangeRateFeederAddress, */
        uint256 bridgeMinSupplyRedeemAmount,
        uint256 bridgeMaxSupplyRedeemAmount,
        uint256 collateralInterestRateFactor,
        uint256 ltv
    ) external onlyCollateralTokenFactory override {
        PoolAsset storage collateralAsset = poolAssets[asset];

        PoolLogic.initCollateralToken(
            collateralAsset, // struct
            asset, // underlying
            wrapped, // collateral asset
            bridgeAddress,
            /* bridgeExchangeRateFeederAddress, */
            bridgeMinSupplyRedeemAmount,
            bridgeMaxSupplyRedeemAmount,
            collateralInterestRateFactor,
            ltv
        );
        addPoolAssetToListInternal(asset);
        addCollateralAssetToListInternal(asset);
        emit CollateralTokenInit(
            asset,
            wrapped
        );
    }

    function initcToken(
        address asset,
        address wrapped,
        address bridgeAddress,
        uint256 bridgeMinSupplyRedeemAmount,
        uint256 depositsSuppliedInterestRateFactor
    ) external onlySavingsTokenFactory override {
        PoolAsset storage poolAsset = poolAssets[asset];
        PoolLogic.initcToken(
            poolAsset,
            asset,
            wrapped,
            bridgeAddress,
            bridgeMinSupplyRedeemAmount,
            depositsSuppliedInterestRateFactor
        );
        addPoolAssetToListInternal(asset);
        addSavingsAssetToListInternal(asset);
        emit SavingsTokenInit(
            asset,
            wrapped
        );
    }

    function initDebtToken(
        address asset,
        address debtWrappedAsset,
        uint256 debtInterestRateFactor,
        uint256 loanThresholdSeconds
    ) external onlyDebtTokenFactory override {
        PoolAsset storage poolAsset = poolAssets[asset];
        PoolLogic.initDebtToken(
            poolAsset,
            debtWrappedAsset,
            debtInterestRateFactor,
            loanThresholdSeconds
        );
    }

    function addCollateralAssetToListInternal(address asset) internal {
        uint256 _collateralAssetsCount = collateralAssetsCount;
        bool poolCollateralAssetAlreadyAdded = false;
        for (uint256 i = 0; i < _collateralAssetsCount; i++)
            if (collateralAssetsList[i] == asset) {
                poolCollateralAssetAlreadyAdded = true;
            }
        if (!poolCollateralAssetAlreadyAdded) {
            collateralAssetsList[collateralAssetsCount] = asset;
            collateralAssetsCount = _collateralAssetsCount + 1;
        }
    }

    function addPoolAssetToListInternal(address asset) internal {
        uint256 _poolAssetsCount = poolAssetsCount;
        bool poolAssetAlreadyAdded = false;
        for (uint256 i = 0; i < _poolAssetsCount; i++)
            if (poolAssetsList[i] == asset) {
                poolAssetAlreadyAdded = true;
            }
        if (!poolAssetAlreadyAdded) {
            poolAssetsList[poolAssetsCount] = asset;
            poolAssetsCount = _poolAssetsCount + 1;
        }
    }

    function addSavingsAssetToListInternal(address asset) internal {
        uint256 _savingsAssetsCount = savingsAssetsCount;
        bool poolSavingsAssetAlreadyAdded = false;
        for (uint256 i = 0; i < _savingsAssetsCount; i++)
            if (savingsAssetsList[i] == asset) {
                poolSavingsAssetAlreadyAdded = true;
            }
        if (!poolSavingsAssetAlreadyAdded) {
            savingsAssetsList[savingsAssetsCount] = asset;
            savingsAssetsCount = _savingsAssetsCount + 1;
        }
    }

}
